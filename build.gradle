buildscript {
	dependencies {
		classpath('org.flywaydb:flyway-database-postgresql:10.16.0')
	}
}

plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.2'
	id 'io.spring.dependency-management' version '1.1.4'
	id "org.jooq.jooq-codegen-gradle" version "3.19.10"
	id "org.flywaydb.flyway" version "10.16.0"
}

group = 'com.nemo'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '21'
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-websocket'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.+'

	implementation 'org.openpnp:opencv:4.9.0-0'

	implementation 'org.flywaydb:flyway-core:10.+'
	runtimeOnly 'org.flywaydb:flyway-database-postgresql:10.+'

	implementation 'org.springframework.boot:spring-boot-starter-jooq'

	runtimeOnly 'org.postgresql:postgresql:42.+'
	jooqCodegen 'org.postgresql:postgresql:42.+'


	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
	useJUnitPlatform()
}

processResources {
	filesMatching("**/application.properties") {
		expand(project.properties)
	}
}

jooqCodegen.dependsOn flywayMigrate

jooq {
	configuration {
		jdbc {
			driver = project.properties.databaseDriver
			url = project.properties.databaseUrl
			user = project.properties.databaseUsername
			password = project.properties.databasePassword
		}
		generator {
			database {
				name = "org.jooq.meta.postgres.PostgresDatabase"
				excludes = "flyway_schema_history"
				inputSchema = project.properties.databaseSchema
			}
			target {
				packageName = "org.jooq.generated"
				directory = "src/main/java"
			}
		}
	}
}

flyway {
	url = project.properties.databaseUrl
	user = project.properties.databaseUsername
	password = project.properties.databasePassword
	driver = project.properties.databaseDriver
	schemas = [project.properties.databaseSchema]
}
